.include "screen.mac"
.macro render_line_data xdirection, ydirection, slope
.local loop, short_axis, long_axis, iterator, no_sleep
.if (.xmatch ({slope}, steep))
  .define long_axis _pl_y
  .define short_axis _pl_x
  .if (.xmatch ({ydirection}, forward))
    .define iterator inx
  .else
    .define iterator dex
  .endif
.else
  .define long_axis _pl_x
  .define short_axis _pl_y
  .if (.xmatch ({xdirection}, forward))
    .define iterator inx
  .else
    .define iterator dex
  .endif
.endif
          ;; load long axis start value into X
          lda long_axis_start_values,y
          tax
          ;; load line length into loop counter Y
          lda long_axis_lengths,y
          tay
loop:
          lda(_lstore),y
          sta short_axis
          stx long_axis
          jsr _plot
          ;; update x register - the long axis coord
          iterator
          dey
          bne loop
.endmacro

.macro render_partial_line xdirection, ydirection, slope
.local loop, short_axis, long_axis, iterator, no_sleep
.if (.xmatch ({slope}, steep))
  .define long_axis _pl_y
  .define short_axis _pl_x
  .if (.xmatch ({ydirection}, forward))
    .define iterator inc long_axis_current_values,x
  .else
    .define iterator dec long_axis_current_values,x
  .endif
.else
  .define long_axis _pl_x
  .define short_axis _pl_y
  .if (.xmatch ({xdirection}, forward))
    .define iterator inc long_axis_current_values,x
  .else
    .define iterator dec long_axis_current_values,x
  .endif
.endif
          lda long_axis_current_values,x
          sta long_axis
          ldy line_data_indices,x
          lda(_lstore),y
          sta short_axis
          jsr _plot
          iterator
          dec line_data_indices,x
.endmacro
